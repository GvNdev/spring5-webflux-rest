plugins {
    id 'java'
    id 'org.springframework.boot' version '4.1.0-M1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.stmc'
version = '0.0.1-SNAPSHOT'
description = 'spring5-webflux-rest'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url = uri('https://repo.spring.io/milestone') }
    maven { url = uri('https://repo.spring.io/snapshot') }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:4.1.0-SNAPSHOT"
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    // Mongo DB reactive
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive-test'

    // Mongo DB
    implementation 'org.springframework.boot:spring-boot-starter-mongodb'
    testImplementation 'org.springframework.boot:spring-boot-starter-mongodb-test'

    // Webflux
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    // JUnit
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

/** JaCoCo */
jacoco {
    toolVersion = '0.8.14'
}

/** Unit tests (como Surefire): excluye IT */
tasks.named('test', Test) {
    useJUnitPlatform()
    exclude '**/*IT.*'
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco-ut.exec")
    }
}

/** Integration tests (como Failsafe): solo *IT */
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests (*IT).'
    group = 'verification'

    useJUnitPlatform()
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    include '**/*IT.*'
    shouldRunAfter tasks.test

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco-it.exec")
    }
}

/** Merge UT + IT (equivalente a goal merge) */
tasks.register('jacocoMergedReport', JacocoReport) {
    dependsOn tasks.named('test'), tasks.named('integrationTest')

    executionData(
            file("$buildDir/jacoco/jacoco-ut.exec"),
            file("$buildDir/jacoco/jacoco-it.exec")
    )

    classDirectories.from(files(sourceSets.main.output))
    sourceDirectories.from(files(sourceSets.main.allSource.srcDirs))

    reports {
        xml.required = true
        xml.outputLocation = file("$buildDir/reports/jacoco/jacoco.xml")
        html.required = true
        html.outputLocation = file("$buildDir/reports/jacoco/html")
        csv.required = false
    }
}

/** check = test + integrationTest + reporte */
tasks.named('check') {
    dependsOn tasks.named('integrationTest')
    dependsOn tasks.named('jacocoMergedReport')
}
